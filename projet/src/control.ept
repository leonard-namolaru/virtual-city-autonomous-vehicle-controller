(* open Globals importe tous les elements du module Globals, 
 * ce qui permet de les utiliser sans utiliser la prefixe "Globals." *)
open Globals

(* Pour l’integration, on suppose que notre integrale est echantillonne tous les dt instants. 
 * Si dt est tres petit, ca nous donne une bonne approximation de l’integrale. 
 * On fait une hypothese sur la variation du signal qu’on integre : dans notre code dt est fixe.
 * Cela signifie que dt secondes (instants physiques) s'ecoulent entre deux instants logiques. *)
const dt : float = 0.01

(* **************************************** DEBUG **************************************** *)

(* Le fichier globals.ept definit le type itielt : type itielt = { act : action; param : float }
 * et fournit la fonction suivante : dbg_action(msg : string; act : action)
 * Mais cette fonction ne permet pas d'imprimer a la fois le type d'operation et la valeur du parametre sur la meme ligne. 
 * A cet effet, la fonction suivante a ete ecrite :   *)
fun dbg_itielt(msg : string; iti : itielt) returns ()
var w0, w1, w2, w3 : Debug.world;
let
  w0 = Debug.d_string(Debug.d_init(), msg);

  switch iti.act
  | Go do w1 = Debug.d_string(w0, "Go ");
  | Turn do w1 = Debug.d_string(w0, "Turn ");
  | Stop do w1 = Debug.d_string(w0, "Stop ");
  end;

  w2 = Debug.d_float(w1, iti.param);
  w3 = Debug.d_string(w2, "\n");
tel

(* La fonction dbg_pid() permet d'imprimer sur une meme ligne tous les coefficients utilises pour le calcul du pid *)
fun dbg_pid(msg : string; kp, ki, kd : float) returns ()
var w0, w1, w2, w3, w4, w5, w6, w7 : Debug.world;
let
  w0 = Debug.d_string(Debug.d_init(), msg);

  w1 = Debug.d_string(w0, "kp = ");
  w2 = Debug.d_float(w1, kp);
  w3 = Debug.d_string(w2, " ki = ");
  w4 = Debug.d_float(w3, ki);
  w5 = Debug.d_string(w4, " kd = ");
  w6 = Debug.d_float(w5, kd);

  w7 = Debug.d_string(w6, "\n");
tel

(* **************************************** PID **************************************** *)

(* PID controller : proportional–integral–derivative controller
 * La noeud pid_controller() recoit comme parametres error et pre_error, qui representent, respectivement,
 * le taux d'erreur à l'instant courant, et le taux d'erreur à l'instant precedent. 
 * De plus, ce noeud recoit les coefficients requis pour le calcul du pid *)
node pid_controller(pre_error, error : float; kp, ki, kd : float) returns (y : float)
let
  y =   kp *. error +.  ki *. Utilities.integrator(error, dt, pre_error) +. kd *. Utilities.derivative(error, dt);
tel


(* Un noeud dont le but est de determiner les valeurs des coefficients kp, ki et kd.
 * Mode de fonctionnement : la valeur de kp est egale a la valeur de la vitesse maximale autorisee, 
 * la valeur de kd augmente progressivement tant que l'augmentation ne provoque pas d'erreur. 
 * Une fois la valeur de kd determinee, le noeud determine la valeur de ki en utilisant la meme methode 
 *
 * Sources : https://www.arnabkumardas.com/platforms/everything-esp/esp32-qtr-8rc-pid-line-follower-robot-v1/ 
 *           https://towardinfinity.medium.com/pid-for-line-follower-11deb3a1a643 *)
node set_kp_ki_kd(after_turn : bool; max_speed, error : float) returns (kp, ki, kd : float)
var ki_determined, kd_determined : bool;
    pre_ki, pre_kd, pre_pre_ki, pre_pre_kd : float;
let
  kp = max_speed;

  reset 
    (* Si la valeur de kd n'a pas encore ete determinee et tant que l'augmentation de kd n'entraine pas d'erreurs *)
    kd = 0.0 -> if (not kd_determined and error = 0.0) 
                    then (pre kd) +. 1.0 
                else (* Si la valeur de kd a deja ete determinee mais que kd est inferieur a 14, nous fixons la valeur a 14 *)
                    Utilities.max_float(14.0, pre kd);

    (* Si ki n'a pas encore ete determine et que kd a ete determine et tant que l'augmentation de ki ne conduit pas a des erreurs *)
    ki = 0.0 -> if (not ki_determined and kd_determined and (error = 0.0)) 
                 or (kd_determined <> false fby kd_determined) (* Ou alternativement - si la valeur de kd vient d'etre determiee, on essaiera d'augmenter ki malgre tout  *)
                    then (pre ki) +. 1.0 
                else 
                    (pre ki);

    (* Nous initialisons pre_kd et pre_pre_kd avec des valeurs differentes car si nous avions initialise les deux avec la meme valeur, 
     * alors au debut pre_kd serait egal a pre_pre_kd, ce qui aurait conduit a ce que la valeur de kd_determined soit true. *)
    pre_ki = -.1.0 fby ki;
    pre_kd = -.1.0 fby kd;

    pre_pre_ki = 1.0 fby pre_ki;
    pre_pre_kd = 1.0 fby pre_kd;

    (* Nous comparons la valeur precedente a la valeur qui a precede la valeur precedente et non a la valeur actuelle afin de ne pas creer d'erreur de causalite *)
    ki_determined = false -> if pre_ki = pre_pre_ki then true else (pre ki_determined);
    kd_determined = false -> if pre_kd = pre_pre_kd then true else (pre kd_determined);
  every after_turn; (* Apres chaque virage, nous determinons a nouveau les valeurs des coefficients *)
tel

(* **************************************** COLOR COMPARISON **************************************** *)
(* Source : https://observablehq.com/@luciyer/euclidian-distance-in-rgb-color-space *)

fun pow_exponent_two(num : int) returns (result : int)
let
  result = num * num;
tel

fun pow_difference_rgb_component(rgb_component1, rgb_component2 : int) returns (result : float)
let
  result = Mathext.float( pow_exponent_two( rgb_component2 - rgb_component1 ) );
tel

node euclidian_distance_rgb_color(color1, color2 : color) returns (d : float)
let
  d = Mathext.sqrt( pow_difference_rgb_component(color2.green, color1.green)
                 +. pow_difference_rgb_component(color2.red, color1.red) 
                 +. pow_difference_rgb_component(color2.blue, color1.blue) );
tel

(* **************************************** SPEED **************************************** *)

fun speed_pid(pid, actual_speed : float) returns (speed : float)
let
  speed = actual_speed -. Utilities.abs(pid);
tel

node get_rspeed(iti : itielt; error, in_red, pid, actual_speed : float ; i:int) returns (rspeed : wheels)
let
  switch iti.act
  | Go do rspeed = if error >. 0.0  then { left = speed_pid(pid, actual_speed) ; right = actual_speed }
                   else                  { left = actual_speed ; right = speed_pid(pid, actual_speed)};
  | Turn do rspeed = if Utilities.countdown(true, i) > 0 then (if iti.param >. 0.0 then { left = 0.0 ; right = actual_speed} else {left = actual_speed ; right = 0.0})
                      else if error >. 0.0  then { left = speed_pid(pid, actual_speed) ; right = actual_speed }
                      else                  { left = actual_speed ; right = speed_pid(pid, actual_speed)};
  | Stop do rspeed = { left = 0.0; right = 0.0};
  end;

tel

(* **************************************** CONTROLLER **************************************** *)

node controller(sens : sensors; iti : itielts) returns (rspeed : wheels; arriving : bool)
var last i : int=0; last action : action = Go; last t : int; last last_sen: color = { red = 128; green = 128; blue = 128 };
    actual_speed, in_red, in_green, error, pre_error, pid, kp, ki, kd : float; last last_speed : float = 0.0; 
    last after_turn : bool = false; 
  let

  in_red =  Mathext.float(sens.s_road.red) /. 255.0;
  in_green =  Mathext.float(sens.s_road.green) /. 255.0;
  error = if sens.s_road.red = 255 and sens.s_road.blue = 0  or (sens.s_road.green = 255 and sens.s_road.blue = 0) then 0.0
          else euclidian_distance_rgb_color( { red = 0; green = 0; blue = 255 }, sens.s_road) /. 255.0 *. (if in_green >. in_red then -. 1.0 else 1.0);

  actual_speed =   if sens.s_front.red = 255 and sens.s_road.red = 255 and sens.s_road.blue = 0 or sens.s_sonar < 80 then 0.0
                   else if (iti[>i<]).act = Go then (iti[>i<]).param *. 360.0 *. Globals.cD /. 100.0 else (0.0 fby actual_speed);
                   
  (kp, ki, kd) =  set_kp_ki_kd(true -> after_turn, actual_speed, error);
  pid = pid_controller(pre_error, error, kp, ki, kd); 
  pre_error = 0.0 fby error;
  last_sen = sens.s_road;
  last_speed = (iti[>i<]).param;

  (* () = dbg_itielt("iti[i] = ", iti[>i<]); *)
  () = dbg_pid("\n", kp, ki, kd);

  automaton
    state Go
      var d : float;
      do
        i = if (sens.s_road.green = 255 and sens.s_road.blue = 0 and (0 fby sens.s_road.green) < 255 and (last last_sen).green < 255) then (last i+1) else (last i);
        action = (iti[>i<]).act;
        arriving = action = Stop;
        rspeed =  get_rspeed(iti[>i<], error, in_red, pid, actual_speed,0);

        (* t pour temps, représente le temps nécéssaire pour tourner sur une roue avec la formule V = D/T
           d est calculé en créeant un faux cercle de centre une des roues et de rayon, distance entre les 2 roues *)
        d = ((2.0 *. Globals.pi *. Globals.cB *. 100.0)) /. (360.0 /. Utilities.abs((iti[>i<]).param));
        t = Mathext.int(d /. (last last_speed));

        after_turn = false;
      until (iti[>i<]).act = Turn then Turn
      
    state Turn
      do 
        (* T est un compteur qui initialisé dans GO qui est le nombre de tick dont la voiture à besoin pour
           tourner selon l'angle du virage demandé *)
        t = last t - 1;
        (* Lorsque le timer est terminé, on passe à l'instruction suivante *)
        i = if (last t>0) then last i else (last i+1);
        action = (iti[>i<]).act;
        arriving = action = Stop;
        rspeed =  get_rspeed(iti[>i<], error, in_red, pid, actual_speed,t);
        after_turn = true;
      until (iti[>i<]).act = Go then Go
  end;
tel


